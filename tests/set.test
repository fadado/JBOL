#########################################################################
# Sets
#
# Tests are groups of three lines: program, input, expected output.
# Blank lines and lines starting with # are ignored.
#########################################################################

#########################################################################
# Set functions

#
# Set construction
#

# The empty set: ∅
length
{ }
0

length
[ ]
0

# Build sets from arrays or strings
include "set"; set(.)
[3,2,1,"k"]
{"1":true,"2":true,"3":true,"k":true}

include "set"; set(.)
"aeiuo"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.)
"uioaaeiouoaeiouae"
{"a":true,"e":true,"i":true,"o":true,"u":true}

# Set members as a list
include "set"; set(.) | keys
"aeiuo"
["a","e","i","o","u"]

# Set cardinality
include "set"; set(.) | length
"aeiuo"
5

#
# Adding and deleting members
#

include "set"; set(.) | del(.e)
"aeiuo"
{"a":true,"i":true,"o":true,"u":true}

include "set"; set(.) | del(.e) | .e = true
"aeiuo"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.) | (.o,.u) = true
"aei"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.) | del(.e,.a,.u)
"aeiuo"
{"i":true,"o":true}

include "set"; set(.) | del(.e) | . += {e:true}
"aeiuo"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.) | del(.e,.a,.u) | . += {u:true,a:true,e:true}
"aeiuo"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.) | del(.e,.a,.u) | (.e,.a,.u) = true
"aeiuo"
{"a":true,"e":true,"i":true,"o":true,"u":true}

include "set"; set(.) | .x = true
"abc"
{"a":true,"b":true,"c":true,"x":true}

#
# Set membership
#

# ∋
include "set"; set(.) | has("k")
[3,2,1,"k"]
true

contains(["k"])
[3,2,1,"k"]
true

# ∋
include "set"; set(.) | .k
[3,2,1,"k"]
true

# ∋
include "set"; set(.) | has("h")
[3,2,1,"k"]
false

contains(["h"])
[3,2,1,"k"]
false

# ∋
include "set"; set(.) | if .h then true else false end
[3,2,1,"k"]
false

# ∋
include "set"; set(.) | .h
[3,2,1,"k"]
null

# ∌
include "set"; set(.) | .h|not
[3,2,1,"k"]
true

contains(["h"])|not
[3,2,1,"k"]
true

# ∌
include "set"; set(.) | .h==null
[3,2,1,"k"]
true

# ∈
include "set"; set(.) as $s | "k" | in($s)
[3,2,1,"k"]
true

inside([3,2,1,"k"])
["k"] 
true

# ∈
include "set"; set(.) as $s | "h" | in($s)
[3,2,1,"k"]
false

inside([3,2,1,"k"])
["h"] 
false

# ∈
include "set"; set(.) as $s | "h" | in($s)
[3,2,1,"k"]
false

# ∉
include "set"; set(.) as $s | "h" | in($s)|not
[3,2,1,"k"]
true

inside([3,2,1,"k"])|not
["h"] 
true

# null => false
include "set"; set(.) | .h//false
[3,2,1,"k"]
false

#
# Common sets operations
#

# ∪
include "set"; set("aiu") + set("eo")
null
{"a":true,"e":true,"i":true,"o":true,"u":true}

# ∪
include "set"; set("aiu") * set("eo")
null
{"a":true,"e":true,"i":true,"o":true,"u":true}

# ∩
include "set"; set("aeiouxyz") | intersection(set("xyz12345"))
null
{"x":true,"y":true,"z":true}

# ∩ ∅
include "set"; (set("xyz") | intersection(set("abc"))) == {}
null
true

# ∩ ∪
include "set"; set("aiu") + set("eo") | intersection(set("eo"))
null
{"o":true,"e":true}

include "set"; set("aeiou") | intersection(set("eo")) + set("aiu")
null
{"a":true,"e":true,"i":true,"o":true,"u":true}

# –
include "set"; set("aeiou") | difference(set("abc"))
null
{"e":true,"i":true,"o":true,"u":true}

["a","e","i","o","c","u"] - ["a","b","c"]
null
["e","i","o","u"]

# ⊂
include "set"; set("xyz") as $a | set("xyz12345") as $b | $a != $b and ($a | intersection($b)) == $a
null
true

# ⊂
include "set"; set("xyz") | inside(set("xyz12345"))
null
true

["x","y","z"] | inside(["x","1","z","y","2","3","4","5"])
null
true

# ⊃ 
include "set"; set("xyz123") as $a | set("xyz") as $b | $a != $b and ($a | intersection($b)) == $b
null
true

include "set"; set(.) | contains(set("k"))
[3,2,1,"k"]
true

# ≡ ∅
. == { }
{ }
true

# ≡
include "set"; set("xyz") == {"y":true,"x":true,"z":true}
null
true

# ≢ ∅
include "set"; set("xyz") != { }
null
true

# vim:ai:sw=2:ts=2:et:syntax=jq
